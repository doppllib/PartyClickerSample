apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'co.doppl.gradle'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    defaultConfig {
        applicationId "com.kgalligan.partyclicker"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:25.1.0'

    //Database ORM
    compile 'co.touchlab.squeaky:squeaky-query:0.5.3'
    annotationProcessor 'co.touchlab.squeaky:squeaky-processor:0.5.3'
    testAnnotationProcessor 'co.touchlab.squeaky:squeaky-processor:0.5.3'
    doppl 'co.touchlab.squeaky:squeaky-query:0.5.3.0'

    //Dagger
    compile 'com.google.dagger:dagger:2.5'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.5'
    testAnnotationProcessor 'com.google.dagger:dagger-compiler:2.5'
    doppl 'co.doppl.com.google.dagger:dagger:2.5.4'

    //Rx
    compile 'io.reactivex:rxandroid:1.2.1'
    doppl 'co.doppl.io.reactivex:rxandroid:1.2.1.4'

    //Test Support
    testCompile 'junit:junit:4.11'
    testCompile "co.doppl.lib:androidbasetest:0.8.0"
    testDoppl "co.doppl.lib:androidbasetest:0.8.0.0"
    testCompile "org.robolectric:robolectric:3.3.2"
    testCompile 'org.mockito:mockito-core:1.9.5'
}

dopplConfig {

    copyMainOutput "../ios/partyclickerframework/main", "../iostest/partyclickerframework/main"
    copyTestOutput "../iostest/partyclickerframework/test"

    copyDependencies true

    translatedPathPrefix 'com.kgalligan.partyclicker', 'P'
    translatedPathPrefix 'com.kgalligan.partyclicker.data', 'PD'
    translatedPathPrefix 'com.kgalligan.partyclicker.presenter', 'PP'
    translatedPathPrefix 'com.kgalligan.partyclicker.test', 'PT'

    generatedTestSourceDirs "build/generated/source/apt/test/debug"

    translatePattern{
        include '**/data/**'
        include '**/presenter/**'
        include '**/test/**'
        include '**/AppManager.java'
    }
}

task runIOSTests(type:Exec, dependsOn: 'dopplDeploy') {
    workingDir '../iostest'
    commandLine 'xcodebuild', 'test', '-project', 'iostest.xcodeproj', '-scheme', 'iostest', '-destination', 'platform=iOS Simulator,name=iPhone 7,OS=10.3'
}
