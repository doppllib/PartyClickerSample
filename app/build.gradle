apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'co.doppl.gradle'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.1"
    defaultConfig {
        applicationId "com.kgalligan.partyclicker"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

def javaCoreVersion = '0.7.5'
def dopplCoreVersion = '0.7.5.0'

dependencies {
    compile 'com.android.support:appcompat-v7:25.1.0'
    testCompile 'junit:junit:4.11'

    compile 'co.touchlab.squeaky:squeaky-query:0.5.1'
    doppl 'co.touchlab.squeaky:squeaky-query:0.5.1.0'

    annotationProcessor 'co.touchlab.squeaky:squeaky-processor:0.5.1'
    testAnnotationProcessor 'co.touchlab.squeaky:squeaky-processor:0.5.1'

    doppl "co.doppl.lib:androidbase:$dopplCoreVersion:doppl"

    doppl 'co.doppl.com.google.dagger:dagger:2.5.2'
    compile 'com.google.dagger:dagger:2.5'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.5'
    testAnnotationProcessor 'com.google.dagger:dagger-compiler:2.5'

    compile 'io.reactivex:rxandroid:1.2.1'

    doppl 'co.doppl.io.reactivex:rxjava:1.2.1.2'
    doppl 'co.doppl.io.reactivex:rxandroid:1.2.1.2'

    testCompile "co.doppl.lib:androidbasetest:$javaCoreVersion"
    testDoppl "co.doppl.lib:androidbasetest:$dopplCoreVersion"

    testCompile "org.robolectric:robolectric:3.3.2"
    testCompile 'org.mockito:mockito-core:1.9.5'
}

dopplConfig {

    copyMainOutput "../ios/partyclickerframework/main", "../iostest/partyclickerframework/main"
    copyTestOutput "../iostest/iostestTests/test"

    mainBridgingHeaderOutput "../ios/ios/Main-Bridging-Header.h"

    copyDependencies true

    translatedPathPrefix 'com.kgalligan.partyclicker', 'P'
    translatedPathPrefix 'com.kgalligan.partyclicker.data', 'PD'
    translatedPathPrefix 'com.kgalligan.partyclicker.presenter', 'PP'
    translatedPathPrefix 'com.kgalligan.partyclicker.test', 'PT'

    generatedTestSourceDirs "build/generated/source/apt/test/debug"

    translatePattern{
        include '**/data/**'
        include '**/presenter/**'
        include '**/test/**'
        include '**/AppManager.java'
    }
}

task runIOSTests(type:Exec, dependsOn: 'dopplDeploy') {
    workingDir '../iostest'

    //on windows:
    commandLine 'xcodebuild', 'test', '-project', 'iostest.xcodeproj', '-scheme', 'iostest', '-destination', 'platform=iOS Simulator,name=iPhone 7,OS=10.3'
}